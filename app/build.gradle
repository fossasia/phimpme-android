plugins {
    id "com.diffplug.gradle.spotless" version "3.16.0"
}

apply plugin: 'com.android.application'
apply plugin: 'com.hiya.jacoco-android'
apply plugin: 'realm-android'
apply plugin: "com.github.b3er.local.properties"

def keystorePropertiesFile = rootProject.file("sample_keystore.properties")
def keystoreProperties = new Properties()
def LOCAL_KEY_PRESENT = project.hasProperty('SIGNING_KEY_FILE') && rootProject.file(SIGNING_KEY_FILE).exists()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "org.fossasia.phimpme"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 14
        versionName "1.11.0"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary true
        resConfigs "en"
        ndk.abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        if (TRAVIS_BUILD) {
            release {
                storeFile KEYSTORE_FILE
                storePassword System.getenv("STORE_PASS")
                keyAlias System.getenv("ALIAS")
                keyPassword System.getenv("KEY_PASS")
            }
        } else if (LOCAL_KEY_PRESENT) {
            release {
                storeFile rootProject.file(SIGNING_KEY_FILE)
                storePassword STORE_PASS
                keyAlias ALIAS
                keyPassword KEY_PASS
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (LOCAL_KEY_PRESENT || TRAVIS_BUILD)
                signingConfig signingConfigs.release
        }
        debug {
            testCoverageEnabled false
        }
    }

    packagingOptions {
        exclude 'META-INF/*'
        exclude 'licenses/javolution.license.TXT'
        exclude 'org/codehaus/classworlds/uberjar/boot/Bootstrapper.class'
    }

    configurations {
        implementation.exclude group: 'org.jetbrains', module: 'annotations'
    }

    lintOptions {
        abortOnError false
        disable "Instantiatable"
    }

    tasks.withType(JavaCompile) {
        configure(options) {
            // TODO
            compilerArgs << "-Xlint:unchecked"
            compilerArgs << "-Xlint:-rawtypes"
        }
    }

    dexOptions {
        jumboMode true
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
}

spotless {
    java {
        googleJavaFormat()
        target '**/*.java'
    }
}

dependencies {
/** Local libraries **/
    implementation project(':openCV')


/** AndroidX **/
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'


/** Third-party **/
    // ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer:r1.5.7'

    // Retrofit
    def retrofitVersion = "2.7.2"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.4.1'

    // Utils
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.0'
    def butterknifeVersion = "10.2.1"
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    def glideVersion = "4.11.0"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.github.bumptech.glide:gifencoder-integration:$glideVersion"
    implementation 'com.yalantis:ucrop:1.5.0'
    implementation 'org.jetbrains:annotations-java5:18.0.0'
    implementation 'com.drewnoakes:metadata-extractor:2.9.1'
    implementation 'id.zelory:compressor:2.1.0'

    // UI
    implementation 'me.jfenn.ColorPickerDialog:base:0.2.1'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.19'
    implementation 'com.github.shchurov:horizontalwheelview:0.9.5'
    implementation 'com.github.chrisbanes:PhotoView:1.2.6'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'
    implementation 'com.github.medyo:android-about-page:1.2.5'

    // Icons
    implementation 'com.mikepenz:iconics-core:2.8.4@aar'
    implementation 'com.mikepenz:google-material-typeface:2.2.0.3.original@aar'
    implementation 'com.mikepenz:community-material-typeface:1.5.54.2@aar'

    // Social
    implementation 'com.twitter.sdk.android:twitter:3.3.0'
    implementation 'org.twitter4j:twitter4j-core:3.0.5'
    implementation 'com.googlecode.flickrj-android:flickrj-android:2.1.0'
    implementation 'com.github.forkerknights:android-pdk:1.2b'
    implementation('com.tumblr:jumblr:0.0.13') {
        exclude module: 'scribe'
    }

    // Cloud
    implementation 'com.dropbox.core:dropbox-core-sdk:3.1.1'
    implementation 'com.box:box-android-sdk:5.0.0'
    implementation 'com.github.nextcloud:android-library:1.5.0'


/** Flavors **/
    debugImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'


/** Testing **/
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
}

